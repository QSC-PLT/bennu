/**
 * @fileOverview Parsers for use specifically with text.
 */
package (
    provide,
    provideString,
    finish,
    parseState,
    parse)
with
    import 'parse/parse' {always, attempt, bind, choicea, getParserState, exec, ExpectError, Memoer, next, Parser, ParserState, Position, token},
    import 'nu/stream' stream{'end': NIL, isEmpty, first, rest}
in {

var trampoline = \f -> {
    var value = f;
    while (value && value._next)
        value = value[0].apply(undefined, value[1]);
    return value;
};

var Request = function(chunk, state, k) {
    this.chunk = chunk;
    this.state = state;
    this.k = k;
};

var Resumable = function(done, chunk, state, k, chunks) {
    this.done = done;
    this.chunk = chunk;
    this.state = state;
    this.k = k;
    this.chunks = chunks;
};

Resumable.prototype.addChunk = \c ->
    new Resumable(
        this.done,
        this.chunk + 1,
        this.state,
        this.k,
        this.chunks.concat(c));

// ResumableState
////////////////////////////////////////
/**
 * Object used to track a Parser's state.
 * 
 * @param input Input to the parser.
 * @param position Current position of head of input.
 * @param [userState] Object that users can thread through parsers.
 */
var ResumableState = function(chunk, state) {
    this.chunk = chunk;
    this.state = state;
};

Object.defineProperties(ResumableState, {
    'input': {
        'get': \() -> this.state.input
    },
    'position': {
        'get': \() -> this.state.position
    },
    'userState': {
        'get': \() -> this.state.userState
    }
});

/**
 * Compares two states for memoization.
 * 
 * This is a part of the seshat id lookup process so the correct position has
 * already been found when this is called.
 */
ResumableState.prototype.eq = \other ->
    other && other.chunk === this.chunk && other.state.eq(other.state);

/**
 * Is the state's input empty.
 */
ResumableState.prototype.isEmpty = \() ->
    this.state.isEmpty();

/**
 * Get the first item of the input.
 */
ResumableState.prototype.first = \() ->
    this.state.first();

/**
 * Get next state object for a given consumed token.
 * 
 * @param tok Token consumed.
 * 
 * @returns New input state for next item in input.
 */
ResumableState.prototype.next = \x -> {
    if (!this._next) {
        var chunk = this.chunk;
        var self = this;
        
        if (isEmpty(rest(this.state.input))) {
            return bind(next(self.state.next(x), getParserState), \s ->
                \_, m, cok ->
                    new Request(
                        chunk + 1,
                        s,
                        \i -> cok(x, new ResumableState(chunk + 1, s.setInput(i)), m)));
        }
        this._next = bind(next(self.state.next(x), getParserState), \s ->
            \_, m, cok ->
                cok(x, new ResumableState(
                    chunk,
                    s),
                m));
    }
    return this._next;
};

/**
 * Create a new state with input.
 * 
 * @param input New input.
 */
ResumableState.prototype.setInput = \input ->
    new ResumableState(this.chunk, this.state.setInput(input));

/**
 * Create a new state with position.
 * 
 * @param position New position.
 */
ResumableState.prototype.setPosition = \position ->
    new ResumableState(this.chunk, this.state.setPosition(position));

/**
 * Create a new state with userState.
 * 
 * @param userState New user state.
 */
ResumableState.prototype.setUserState = \userState ->
    new ResumableState(this.chunk, this.state.setUserState(userState));

/* Running
 ******************************************************************************/
provide = \r, c -> {
    if (r.done)
        return r;
    
    var r2 = r.addChunk(c);
    var result = trampoline(r2.k(r2.state.setInput(c)));
    while (result instanceof Request) {
        if (result.chunk >= r2.chunks.length)
            return new Resumable(
                false,
                r2.chunk,
                result.state,
                \x -> result.k(x.input),
                r2.chunks);
        result = trampoline(result.k(r2.chunks[result.chunk]));
    }
    return result;
};

provideString = \r, s ->
    provide(r, stream.from(s));

finish = \r ->
    !r.done ?
        finish(trampoline(r.k(r.state))) :
        r.k(r.state);

/* Running
 ******************************************************************************/

        
/** 
 * Run a given parser with a given state.
 * 
 * @param p Parser to run.
 * @param state ResumableState to run parser against.
 * 
 * @return Result from the parser.
 */
parseState = \p, state ->
    new Resumable(
            false,
        0,
        new ResumableState(0, state),
        \s -> let
            ok = \x, s -> new Resumable(true, -1, s, \ -> x),
            err = \x, s -> new Resumable(true, -1, s, \ -> { throw x; })
        in
            exec(p, s, Memoer.empty, ok, err, ok, err),
        []);

/**
 * Run parser 'p' against an input string.
 * 
 * @param p Parser to run.
 * @param input Array-like object of characters to run the parser against.
 * @param [ud] User data.

 * @return Result from the parser.
 */
parse = \p, ud ->
    parseState(
        p,
        new ParserState(
            NIL,
            Position.initial,
            ud));

}