/**
 * @fileOverview Running parsers incrementally.
 */
package (
// Operations
    provide
    provideString
    finish
    
// Incremental Parser Running
    parseIncState
    parseInc

    runIncState
    runInc

// Many Parser running
    runManyState
    runManyStream
    runMany)
with
    import './parse' {
        always
        bind
        getParserState
        Memoer
        next
        optional
        parseState
        ParserState
        Position
        runState
        Tail
        trampoline},
    import 'nu-stream::stream' stream#{
        'from': streamFrom
        isEmpty
        first
        NIL
        rest
        memoStream}
in {

/* Records
 ******************************************************************************/
// Request
////////////////////////////////////////
var Request = function \chunk, k =self-> {
    self.chunk = chunk;
    self.k = k;
};

// Session
////////////////////////////////////////
var Session = function \done, k, chunks =self-> {
    self.done = done;
    self.k = k;
    self.chunks = chunks;
};

Session.prototype.addChunk = \c =self->
    new Session(
        self.done,
        self.k,
        self.chunks.concat(c));

Session.prototype.hasChunk = \c =self->
    (c < self.chunks.length);

Session.prototype.getChunk = \c =self->
    self.chunks.(c);

/* IncrementalState
 ******************************************************************************/
/**
 * State for an incremental parser.
 * 
 * Forwards all operations to an inner state.
 * 
 * @param chunk Identifier for the chunk the state is currently operating on.
 * @param state Inner parser state.
 */
var IncrementalState = function \chunk, state =self-> {
    self.chunk = chunk;
    self.state = state;
};

Object.defineProperties(IncrementalState.prototype, {
    'input': {
        'get': \() =self-> self.state.input
    },
    'position': {
        'get': \() =self-> self.state.position
    },
    'userState': {
        'get': \() =self-> self.state.userState
    }
});

IncrementalState.prototype.eq = \other =self->
    other && other.chunk === self.chunk && self.state.eq(other.state);

IncrementalState.prototype.isEmpty = \() =self->
    self.state.isEmpty();

IncrementalState.prototype.first = \() =self->
    self.state.first();

IncrementalState.prototype.next = \x =self-> {
    if (!self._next) {
        var chunk = self.chunk;
        
        self._next = bind(
            next(self.state.next(x), getParserState),
            \innerState ->
                ?innerState.isEmpty()
                    :\_, m, cok ->
                        new Request(
                            chunk + 1,
                            \i -> cok(x, new IncrementalState(chunk + 1, innerState.setInput(i)), m))
                    :let state = new IncrementalState(chunk, innerState) in
                        \_, m, cok -> cok(x, state, m));
    }
    return self._next;
};

IncrementalState.prototype.setInput = \input =self->
    new IncrementalState(
        self.chunk,
        self.state.setInput(input));

IncrementalState.prototype.setPosition = \position =self->
    new IncrementalState(
        self.chunk,
        self.state.setPosition(position));

IncrementalState.prototype.setUserState = \userState =self->
    new IncrementalState(
        self.chunk,
        self.state.setUserState(userState));

/* Operations
 ******************************************************************************/
/**
 * Always add any chunk passed in, even empty ones.
 */
var forceProvide = \r c -> {
    if (r.done)
        return r;
    
    var r2 = r.addChunk(c);
    
    var result = r2.k(c) |> trampoline;
    while (result instanceof Request && r2.hasChunk(result.chunk))
        result = result.k(r2.getChunk(result.chunk)) |> trampoline;
    
    return ?result instanceof Request
        :new Session(false, result.k, r2.chunks)
        :result;
};

/**
 * Feed a new chunk of input to an incremental parser.
 * 
 * Will parse as much as possible before returning.
 * 
 * @param r Resumable state
 * @param c Nu stream of input for chunk.
 * 
 * @return New resumable state for parser after input has been received.
 */
provide = \r c ->
    ?isEmpty c
        :r
        :forceProvide(r, c);

/**
 * Feed an array-like to an incremental parser.
 * 
 * @param r Resumable state.
 * @param input Array like object of input.
 */
provideString = \r input ->
    provide(r, streamFrom input);

/**
 * Signal that an incremental parsing is complete.
 * 
 * Returns the result of the parsing.
 * 
 * @param r Resumable state.
 */
finish = let
    complete = \r -> r.k()
in
     \r -> complete <| forceProvide(r, NIL);

/* Incremental Creation
 ******************************************************************************/
/** 
 * Begin incremental parsing for `p` with a custom inner state.
 * 
 * @param p Parser.
 * @param state Inner parser state.
 * @param ok Success continuation.
 * @param err Error continuation.
 * 
 * @return Opaque state object for a partially evaluated parser.
 */
parseIncState = \p state ok err -> let 
    pok = \x s -> new Session(true, ok@(x, s)),
    perr = \x s -> new Session(true, err@(x, s))
in
    provide(
        new Session(
            false,
            \i -> parseState(p, new IncrementalState(0, state.setInput(i)), pok, perr),
            []),
        state.input);

/**
 * Begin incrementally parsing `p`.
 * 
 * @param p Parser.
 * @param [ud] User data.
 * 
 * @see parseIncState
 */
parseInc = \p ud ok err ->
    parseIncState(
        p,
        new ParserState(NIL, Position.initial, ud),
        ok,
        err);

/**
 * Begin incrementally parsing `p`
 * 
 * Returns successful results directly, throws error results.
 * 
 * @param p Parser.
 * @param state State.
 */
runIncState = let
    ok = \x -> x,
    err = \x -> { throw x; }
in
    \p state -> 
        parseIncState(p, state, ok, err);

/**
 * Begin incrementally parsing `p`.
 * 
 * @param p Parser.
 * @param ud User data.
 */
runInc = \p ud ->
    runIncState(
        p,
        new ParserState(
            NIL,
            Position.initial,
            ud));

/* Run Many
 ******************************************************************************/
/**
 * Run parser 'p' zero or more times to produce a lazy stream of results.
 * 
 * Similar in operation to the 'many' parser, but is not a
 * combinatorial parser.
 * 
 * Result may be an infinite stream.
 * 
 * @param p Parser to run zero or more times.
 * @param state State to run parser against.
 * 
 * @return Lazy stream of results.
 */
runManyState = \p state toEnd -> let
    manyP = optional(NIL,
        \state m cok cerr eok eerr ->
            new Tail(p, state, m,
                \x state m -> cok(memoStream(x, runState@(manyP, state, m))),
                cerr,
                \x state m -> eok(memoStream(x, runState@(manyP, state, m))),
                eerr))
in
    runState(manyP, state);

/**
 * Run parser 'p' zero or more times to produce a lazy stream or results.
 * 
 * Supplies own state.
 * 
 * @param p Parser to run.
 * @param s Nu style stream supporting first and rest.
 * @param [ud] User data.
 *
 * @see runManyState
 */
runManyStream = \p s ud ->
    runManyState(p, new ParserState(s, Position.initial, ud));

/**
 * Run parser 'p' zero or more times against an input string to produce a lazy
 * stream or results.
 * 
 * @param p Parser to run.
 * @param input Array-like object of characters to run the parser against.
 * @param [ud] User data.
 *
 * @see runManyStream
 */
runMany = \p input ud ->
    runManyStream(p, streamFrom input, ud);

}